PREFIX pproc: <http://contsem.unizar.es/def/sector-publico/pproc#>
PREFIX s: <http://schema.org/>
PREFIX pc: <http://purl.org/procurement/public-contracts#>
PREFIX gr: <http://purl.org/goodrelations/v1#>
PREFIX dcterms: <http://purl.org/dc/terms/>

SELECT DISTINCT ?contrato ?nombreContrato ?type ?adjudicatario ?impLicitacionSinIVA ?impLicitacionConIVA ?impCanon 
?impAdjudicacionSinIVA ?impAdjudicacionConIVA ?procedimiento ?entidad ?servicio ?numLicitadores 
WHERE {
  ?contrato dcterms:title ?nombreContrato;
  pproc:contractObject ?contractObject;
  pproc:managingDepartment ?managing.
  ?tender pc:supplier ?supplier.
  ?managing dcterms:title ?servicio.
  ?supplier s:name ?adjudicatario.
  ?contractObject pproc:contractEconomicConditions ?contractEconomicConditions.
  OPTIONAL{
    ?contractEconomicConditions pproc:budgetPrice ?budgetPriceNOVAT;
    pproc:budgetPrice ?budgetPriceVAT.
    ?budgetPriceNOVAT gr:hasCurrencyValue ?impLicitacionSinIVA;
    gr:valueAddedTaxIncluded "false"^^xsd:boolean.
    ?budgetPriceVAT gr:hasCurrencyValue ?impLicitacionConIVA;
    gr:valueAddedTaxIncluded "true"^^xsd:boolean.
  }
  OPTIONAL {
    ?contractEconomicConditions pproc:feePrice ?feePrice.
    ?feePrice gr:hasCurrencyValue ?impCanon;
    gr:valueAddedTaxIncluded "false"^^xsd:boolean.
  }
  ?tender pc:offeredPrice ?offeredPriceNOVAT;
  pc:offeredPrice ?offeredPriceVAT.
  ?offeredPriceNOVAT gr:hasCurrencyValue ?impAdjudicacionSinIVA;
  gr:valueAddedTaxIncluded "false"^^xsd:boolean.
  ?offeredPriceVAT gr:hasCurrencyValue ?impAdjudicacionConIVA;
  gr:valueAddedTaxIncluded "true"^^xsd:boolean.
    {
      SELECT DISTINCT ?contrato ?procedimiento ?type ?tender ?entidad ?servicio 
        WHERE {
          ?contrato a pproc:Contract;
          a ?type;
          pproc:contractProcedureSpecifications/pproc:procedureType ?procedimiento;
          pc:contractingAuthority/dcterms:title ?entidad;
          pc:tender ?tender;
          pproc:managingDepartment ?managing.
          ?tender a pproc:FormalizedTender;
          pproc:formalizedDate ?formalizedDate.
          ?managing dcterms:title ?servicio.
          FILTER  (regex(?formalizedDate, "2014")) 
        } 
    } 
    {
      SELECT DISTINCT ?contrato count(?tender) AS ?numLicitadores 
        WHERE {
          ?contrato a pproc:Contract;
          a ?type;pc:tender ?tender.
        } 
      GROUP BY ?contrato
    }
}
